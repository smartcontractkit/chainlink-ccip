# Default: show available recipes
default:
    just --list

# Run go mod tidy
tidy:
    go mod tidy

clean:
    rm -rf compose/ blockscout/

# Remove all the chaos experiments
rm-chaos:
    kubectl delete networkchaos --all -A
    kubectl delete podchaos --all -A
    kubectl delete iochaos --all -A
    kubectl delete timechaos --all -A
    kubectl delete stresschaos --all -A
    kubectl delete dnschaos --all -A
    kubectl delete kernelchaos --all -A

# Format all go files
fmt:
    golangci-lint fmt --config=.golangci.yaml

# Run golangci-lint
lint:
    golangci-lint run --whole-files --output.text.path stdout --max-same-issues=0 --config=.golangci.yaml

# Install pre-commit hooks
pre-commit:
    pre-commit install

# Builds and installs new version of CLI locally
cli:
    go install cmd/ccip.go

# Build all the services (hot-reload dev mode)
build-docker-dev:
    @just build-fakes-docker
    @just build-indexer-docker-dev
    @just build-jd-docker

# Remove all the dev images
clean-docker-dev:
    docker rmi indexer:dev indexer:latest ccip-fakes:local job-distributor:local >/dev/null 2>&1 || true
    rm -rf job-distributor/

# Build all the services for production
build-docker-prod:
    @just build-fakes-docker
    @just build-indexer-docker-prod

# Build fakes image
build-fakes-docker:
    cd fakes && docker build -t ccip-fakes:local . || cd -

# Build Indexer dev image
build-indexer-docker-dev:
    cd indexer && docker build -f Dockerfile.dev -t indexer:dev . || cd -

# Build Indexer production image
build-indexer-docker-prod:
    cd indexer && docker build -f Dockerfile -t indexer:dev . || cd -

# Clone and build job-distributor Docker image
build-jd-docker:
    #!/usr/bin/env bash
    set -e
    REPO_URL="https://github.com/smartcontractkit/job-distributor"
    REPO_DIR="job-distributor/"
    IMAGE_NAME="job-distributor:local"
    DOCKERFILE="e2e/Dockerfile.e2e"
    if docker image inspect "$IMAGE_NAME" >/dev/null 2>&1; then
        echo "Docker image $IMAGE_NAME already exists, skipping build..."
    else
        echo "Cloning repository..."
        git clone "$REPO_URL" "$REPO_DIR"
        echo "Building Docker image..."
        cd "$REPO_DIR" && docker build -t "$IMAGE_NAME" -f "$DOCKERFILE" .
        cd ..
        rm -rf job-distributor/
    fi
    echo "Done!"

# Sets MinIO alias for easier use of 'mc' CLI
minio-alias:
   mc alias set minio http://localhost:9000 mYrAnD0mAcc3ssK3y2024 s3cR3tK3yL0ngEn0ughF0rMinI0Testing2024!