
# Default: show available recipes
default:
    just --list

clean:
    rm -rf compose/ blockscout/

# Install pre-commit hooks
pre-commit:
    pre-commit install

# Network-specific implementation repository prefix
REPO_BASE := "https://github.com/smartcontractkit/chainlink-"

# Download chainlink implementations for devenv
# Usage: just download-implementations ton solana
download-implementations *ARGS:
    #!/usr/bin/env bash
    set -e

    for product in {{ARGS}}; do
        repo_url={{REPO_BASE}}$product
        target_dir="../../chainlink-$product"

        echo "Downloading $product to $target_dir..."

        if [ -d "$target_dir" ]; then
            echo "Directory $target_dir already exists, skipping..."
        else
            git clone "$repo_url" "$target_dir"
            echo "Successfully downloaded $product"
        fi
    done

# Clone and build job-distributor Docker image
build-jd-docker:
    #!/usr/bin/env bash
    set -e
    REPO_URL="git@github.com:smartcontractkit/job-distributor.git"
    REPO_DIR="job-distributor/"
    IMAGE_NAME="job-distributor:local"
    DOCKERFILE="e2e/Dockerfile.e2e"
    if docker image inspect "$IMAGE_NAME" >/dev/null 2>&1; then
        echo "Docker image $IMAGE_NAME already exists, skipping build..."
    else
        echo "Cloning repository... check for 2-factor authentication"
        git clone "$REPO_URL" "$REPO_DIR"
        echo "Building Docker image..."
        cd "$REPO_DIR" && docker build -t "$IMAGE_NAME" -f "$DOCKERFILE" .
        cd ..
        rm -rf job-distributor/
    fi
    echo "Done!"

# Rebuild CLI
cli:
    pushd cmd/ccip > /dev/null && go install -ldflags="-X main.Version=1.0.0" . && popd > /dev/null
